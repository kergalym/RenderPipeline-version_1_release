#version 400

#pragma optionNV (unroll all)

#pragma include "Includes/Configuration.include"
#pragma include "Includes/PoissonDisk.include"

in vec2 texcoord;
out vec4 resultColor;

uniform sampler2D colorTex;
uniform sampler2D dilatedVelocityTex;
uniform float frameDelta;

void main() {

    ivec2 screenCoord = ivec2(gl_FragCoord.xy);

    #if defined(DEBUG_DISABLE_MOTIONBLUR)
        vec4 sourceColor = texture(colorTex, texcoord);
        resultColor = vec4(sourceColor);
    #else
        vec2 velocity = texture(dilatedVelocityTex,texcoord).xy * 255.0;

        // Don't use motion blur if the velocity is very small
        if (length(velocity) < 0.04) {
            resultColor = texture(colorTex, texcoord);
        } else {

            const int numSamples = MOTION_BLUR_SAMPLES;
            vec2 pixelSize = 1.0 / vec2(WINDOW_WIDTH, WINDOW_HEIGHT);
            vec4 colorSum = vec4(0);
            vec2 blurDirection = velocity * pixelSize * 0.04 * MOTION_BLUR_FACTOR / frameDelta;

            for (int i = 0; i < numSamples; i++) {
                vec2 sampleCoord = texcoord + (float(i)/float(numSamples) - 0.5) * blurDirection;
                colorSum += texture(colorTex, sampleCoord);
            }

            resultColor = vec4(colorSum / numSamples);
        }
    #endif
}